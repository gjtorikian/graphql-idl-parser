use ast::{Definition, Field, FieldType, GraphQLType};

grammar;

pub schema: Vec<Definition> =
  <Definition*> => <>;

Definition: Definition = {
  <s:Description?> <g:GQLTypeWithFields> <n:Name> "{" <f:(<Field>)*> "}" => {
    Definition {
      description: match s {
        None => String::new(),
        Some(s) => {
            s
        }
      },
      typename: g,
      name: n,
      implements: Vec::new(),
      fields: f
    }
  },
  <s:Description?> <g:GQLTypeWithFields> <n:Name> "implements" <v:(<Name> ",")*> <e:Name?> "{" <f:(<Field>)*> "}" => {
    Definition {
      description: match s {
        None => String::new(),
        Some(s) => {
            s
        }
      },
      typename: g,
      name: n,
      implements: match e {
          None => v,
          Some(e) => {
              let mut v = v;
              v.push(e);
              v
          }
      },
      fields: f
    }
  },
  <s:Description?> <g:GQLTypeWithoutFields> <n:Name> => {
    Definition {
      description: match s {
        None => String::new(),
        Some(s) => {
            s
        }
      },
      typename: g,
      name: n,
      implements: Vec::new(),
      fields: Vec::new()
    }
  }
};

Fields: Vec<Field> = {
  <f:(<Field>)*> => f
};

Field: Field = {
  <s:Description?> <n:Name> ":" <t:FieldType> <r:"!"?> => {
    Field {
      description: match s {
        None => String::new(),
        Some(s) => {
            s
        }
      },
      name: n,
      fieldtype: t,
      nullable: match r {
        None => true,
        Some(r) => false
      }
    }
  }
};

FieldType: FieldType = {
  <n:Name> => {
    FieldType {
      list: false,
      name: n,
      nullable: true
    }
  },
  "[" <n:Name> <r:"!"?> "]" => {
    FieldType {
      name: n,
      list: true,
      nullable: match r {
        None => true,
        Some(r) => false
      }
    }
  }
};

GQLTypeWithFields: GraphQLType = {
  "type" => GraphQLType::ObjectType,
  "interface" => GraphQLType::InterfaceType,
  "input" => GraphQLType::InputObjectType
};

GQLTypeWithoutFields: GraphQLType = {
  "scalar" => GraphQLType::ScalarType,
  "union" => GraphQLType::UnionType
};

Name: String = r"[_A-Za-z][_0-9A-Za-z]*" => <>.to_string();
Description: String = <s:r"#\s*[^\n]*"> => s[1..s.len()].trim().to_string();
