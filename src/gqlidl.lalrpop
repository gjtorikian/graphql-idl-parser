use ast::*;

grammar;

pub schema: Vec<GraphQLType> =
    <GraphQLType*> => <>;

GraphQLType: GraphQLType = {
    <d:Description?> "scalar" <n:Name> => {
        GraphQLType::ScalarType(GraphQLScalar::new(d, n))
    },
    <d:Description?> "type" <n:Name> "{" <f:(<Fields>)*> "}" => {
        GraphQLType::ObjectType(GraphQLObject::new(d, n, Vec::new(), f))
    },
    <d:Description?> "type" <n:Name> "implements" <i:(<Implements>)> "{" <f:(<Fields>)*> "}" => {
        GraphQLType::ObjectType(GraphQLObject::new(d, n, i, f))
    },
    <d:Description?> "enum" <n:Name> "{" <f:(<Values>)+> "}" => {
        GraphQLType::EnumType(GraphQLEnum::new(d, n, f))
    },
};

Implements: Vec<String> = {
    <v:(<Name> ",")*> <e:Name?> => {
        match e {
            None => v,
            Some(e) => {
                let mut v = v;
                v.push(e);
                v
            }
        }
    }
};

Fields: GraphQLField = {
    <d:Description?> <n:Name> ":" <t:FieldType> <e:Deprecated?> <r:DeprecatedReason?> => {
        let deprecated = match e {
                          None => false,
                          Some(e) => true
                      };
        GraphQLField::new(d, n, t, Vec::new(), deprecated, r)
    },
    <d:Description?> <n:Name> "(" <a:(<Arguments>)*> ")" ":" <t:FieldType> <e:Deprecated?> <r:DeprecatedReason?> => {
        let deprecated = match e {
                          None => false,
                          Some(e) => true
                      };
        GraphQLField::new(d, n, t, a, deprecated, r)
    },
};

Values: GraphQLValue = {
    <d:Description?> <n:Name> => {
        GraphQLValue::new(d, n)
    }
};

FieldType: FieldType = {
    <n:Name> <r:"!"?> => {
        FieldType {
            name: n,
            info: match r {
              None => TypeInfo::Nullable,
              Some(r) => TypeInfo::NonNullable,
            }
        }
    },
    "[" <n:Name> <r:"!"?> "]" <l:"!"?> => {
        FieldType {
            name: n,
            info: match r {
              None => match l {
                  None => TypeInfo::NullableListNullableElements,
                  Some(l) => TypeInfo::NonNullableListNullableElements
              },
              Some(r) => match l {
                  None => TypeInfo::NonNullableListNullableElements,
                  Some(l) => TypeInfo::NonNullableListNonNullableElements
              }
            }
        }
    }
};

Arguments: GraphQLArgument = {
    <d:Description?> <n:Name> ":" <t:FieldType> => {
      GraphQLArgument::new(d, n, t)
    }
};

Deprecated: String = {
    "@deprecated" => <>.to_string()
};

DeprecatedReason: String = {
    "(reason: " <s:r#""[^"]*""#> ")" => {
      s[1..s.len()-1].to_string()
    }
};

Name: String = r"[_A-Za-z][_0-9A-Za-z]*" => <>.to_owned();
Description: String = <s:r"#\s*[^\n]*"> => s[1..s.len()].trim().to_owned();
