use ast::*;

grammar;

pub schema: Vec<GraphQLType> =
    <GraphQLType*> => <>;

GraphQLType: GraphQLType = {
    <d:Description*> "scalar" <n:Name> => {
        GraphQLType::ScalarType(GraphQLScalar::new(d, n))
    },
    <d:Description*> "type" <n:Name> "{" <f:(<Field>)*> "}" => {
        GraphQLType::ObjectType(GraphQLObject::new(d, n, vec![], f))
    },
    <d:Description*> "type" <n:Name> "implements" <i:Comma<Name>> "{" <f:(<Field>)*> "}" => {
        GraphQLType::ObjectType(GraphQLObject::new(d, n, i, f))
    },
    <d:Description*> "enum" <n:Name> "{" <f:(<Value>)+> "}" => {
        GraphQLType::EnumType(GraphQLEnum::new(d, n, f))
    },
    <d:Description*> "interface" <n:Name> "{" <f:(<Field>)*> "}" => {
        GraphQLType::InterfaceType(GraphQLInterface::new(d, n, f))
    },
    <d:Description*> "union" <n:Name> "=" <t:Pipe<Name>> => {
        GraphQLType::UnionType(GraphQLUnion::new(d, n, t))
    },
    <d:Description*> "input" <n:Name> "{" <f:(<Field>)*> "}" => {
        GraphQLType::InputObjectType(GraphQLInputObject::new(d, n, f))
    },
};

Field: GraphQLField = {
    <d:Description*> <n:FieldName> ":" <t:FieldType> <e:Deprecated?> <r:DeprecatedReason?> => {
        let deprecated = match e {
                          None => false,
                          Some(e) => true
                      };
        GraphQLField::new(d, n, t, vec![], deprecated, r)
    },
    <d:Description*> <n:FieldName> "(" <a:(<Argument>)*> ")" ":" <t:FieldType> <e:Deprecated?> <r:DeprecatedReason?> => {
        let deprecated = match e {
                          None => false,
                          Some(e) => true
                      };
        GraphQLField::new(d, n, t, a, deprecated, r)
    },
};

Value: GraphQLValue = {
    <d:Description*> <n:Name> => {
        GraphQLValue::new(d, n)
    }
};

FieldType: FieldType = {
    <n:Name> <r:"!"?> => {
        FieldType {
            name: n,
            info: match r {
              None => TypeInfo::Nullable,
              Some(r) => TypeInfo::NonNullable,
            }
        }
    },
    "[" <n:Name> <r:"!"?> "]" <l:"!"?> => {
        FieldType {
            name: n,
            info: match r {
              None => match l {
                  None => TypeInfo::NullableListNullableElements,
                  Some(l) => TypeInfo::NonNullableListNullableElements
              },
              Some(r) => match l {
                  None => TypeInfo::NonNullableListNullableElements,
                  Some(l) => TypeInfo::NonNullableListNonNullableElements
              }
            }
        }
    }
};

Argument: GraphQLArgument = {
    <d:Description*> <n:FieldName> ":" <t:FieldType> => {
      GraphQLArgument::new(d, n, t)
    }
};

Deprecated: String = {
    "@deprecated" => <>.to_string()
};

DeprecatedReason: String = {
    "(reason: " <s:r#""[^"]*""#> ")" => {
      s[1..s.len()-1].to_string()
    }
};

Comma<T>: Vec<T> = {
    Comma1<T>? => <>.unwrap_or(vec![])
};

Comma1<T>: Vec<T> = {
    <t:T> => vec![t],
    <v:Comma<T>> "," <t:T> => {
        let mut v = v;
        v.push(t);
        v
    }
};

Pipe<T>: Vec<T> = {
    Pipe1<T>? => <>.unwrap_or(vec![])
};

Pipe1<T>: Vec<T> = {
    <t:T> => vec![t],
    <v:Pipe<T>> "|" <t:T> => {
        let mut v = v;
        v.push(t);
        v
    }
};

Name: String = r"[_A-Za-z][_0-9A-Za-z]*" => <>.to_owned();
FieldName: String = {
    r"[_A-Za-z][_0-9A-Za-z]*" => <>.to_owned(),
    "scalar"  => "scalar".to_owned(),
    "type"  => "type".to_owned(),
    "enum"  => "enum".to_owned(),
    "interface"  => "interface".to_owned(),
    "union"  => "union".to_owned(),
    "input"  => "input".to_owned(),
};

Description: String = <s:r"#\s*.+"> => s[1..s.len()].trim().to_owned();
